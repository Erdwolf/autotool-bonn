module Java (
    block,
    vars,
    tipe,
    consProto,
    args,
    consArgs,
    ident,
    vWriteFile,
    string,
    upcase,
    locase
) where

import System.Time
import Text.PrettyPrint.HughesPJ
import Data.Char
import Basic
import Types

block :: [Doc] -> Doc
block xs = vcat [text "{", nest 4 (vcat xs), text "}", text ""]

vars :: [AType] -> Doc
vars [] = empty
vars xs = text "<" <> cat (punctuate (text ", ") (map tipe xs)) <> text ">"

tipe :: AType -> Doc
tipe (AVar s) = text (map toUpper s)
tipe (AType s xs) = text s <> vars xs

consProto :: ACons -> Doc
consProto con = hcat [
    text (consName con),
    text "(" <> cat (punctuate (text ", ") (args (consArgs con))) <> text ")"]

consArgs :: ACons -> [(String, AType)]
consArgs (ACons _ tys) = zip ["field" ++ show i | i <- [1..]] tys
consArgs (ARec _ tys) = tys

args :: [(String, AType)] -> [Doc]
args = map (\(nm, ty) -> tipe (unbox ty) <+> ident [nm])

ident :: [String] -> Doc
ident = text . locase . concat . map upcase . concatMap split_ where
    split_ [] = []
    split_ xs = let (a, b) = span (/= '_') xs
                in  a : split_ (drop 1 b)

upcase :: String -> String
upcase [] = []
upcase (x:xs) = toUpper x : xs

locase :: String -> String
locase [] = []
locase (x:xs) = toLower x : xs

-- this does not really belong here
vWriteFile fn text = do
    today <- show `fmap` getClockTime
    putStrLn $ "  Writing '" ++ fn ++ "'."
    writeFile fn $ unlines [
        "/*",
        " * Warning: This is a generated file. Edit at your own risk.",
        " * generated by Gen.hs on " ++ today ++ ".",
        " */",
        ""
     ] ++ text

string :: String -> Doc
string s = text (concat ["\"", concatMap quote s, "\""])

quote :: Char -> String
quote '"' = "\\\""
quote '\\' = "\\\\"
quote '\n' = "\\n"
quote c = c : ""
